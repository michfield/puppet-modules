@ECHO OFF
:: Some variables
SET "EMBEDDED_DIR=%~dp0\..\embedded"

:: Did we interrupted Vagrant execution, last time?
:: Then we probably have other saved variables available, so let's restore them

IF "%VAGRANT_EXECUTING%"=="1" (
  IF NOT "%VAGRANT_PATH_SAVED%"==""     SET "PATH=%VAGRANT_PATH_SAVED%"
  IF NOT "%VAGRANT_GEM_HOME_SAVED%"=="" SET "GEM_HOME=%VAGRANT_GEM_HOME_SAVED%"
  IF NOT "%VAGRANT_GEM_PATH_SAVED%"=="" SET "GEM_PATH=%VAGRANT_GEM_PATH_SAVED%"
  IF NOT "%VAGRANT_GEMRC_SAVED%"==""    SET "GEMRC=%VAGRANT_GEMRC_SAVED%"
  IF NOT "%VAGRANT_RUBYOPT_SAVED%"==""  SET "RUBYOPT=%VAGRANT_RUBYOPT_SAVED%"
)

:: Save some variables we're going to change so we can restore them later
SET "VAGRANT_GEM_HOME_SAVED=%GEM_HOME%"
SET "VAGRANT_GEM_PATH_SAVED=%GEM_PATH%"
SET "VAGRANT_GEMRC_SAVED=%GEMRC%"
SET "VAGRANT_RUBYOPT_SAVED=%RUBYOPT%"

:: Set environmental variables
SET "GEM_HOME=%EMBEDDED_DIR%\gems"
SET "GEM_PATH=%GEM_HOME%;%VAGRANT_GEM_PATH_SAVED%"
SET "GEMRC=%EMBEDDED_DIR%\etc\gemrc"

:: Export an enviromental variable to say we're in a Vagrant installer created environment.
SET "VAGRANT_INSTALLER_ENV=1"
SET "VAGRANT_INSTALLER_EMBEDDED_DIR=%EMBEDDED_DIR%"

:: Say we are still inside running Vagrant
SET "VAGRANT_EXECUTING=1"

:: Save PATH and prepend embedded bin to PATH so we prefer those binaries
SET "VAGRANT_PATH_SAVED=%PATH%"
SET "PATH=%EMBEDDED_DIR%\bin;%EMBEDDED_DIR%\gnuwin32\bin;%PATH%"

:: Remove RUBYOPT, which causes serious problems.
SET RUBYOPT=

:: Run Vagrant...
"%EMBEDDED_DIR%\..\embedded\bin\ruby.exe" "%EMBEDDED_DIR%/../embedded/gems/bin/%~n0" %*

:: Store the exit status so we can re-use it later
SET "VAGRANT_EXIT_STATUS=%ERRORLEVEL%"

:: Restore some environmental variables we changed
SET "GEM_HOME=%VAGRANT_GEM_HOME_SAVED%"
SET "GEM_PATH=%VAGRANT_GEM_PATH_SAVED%"
SET "GEMRC=%VAGRANT_GEMRC_SAVED%"
SET "RUBYOPT=%VAGRANT_RUBYOPT_SAVED%"

:: Restore PATH
SET "PATH=%VAGRANT_PATH_SAVED%"

:: We are not running Vagrant anymore
SET "VAGRANT_EXECUTING="

:: Clean up temporary saved variables
SET "VAGRANT_PATH_SAVED="
SET "VAGRANT_GEM_HOME_SAVED="
SET "VAGRANT_GEM_PATH_SAVED="
SET "VAGRANT_GEMRC_SAVED="
SET "VAGRANT_RUBYOPT_SAVED="

:: Exit with the proper exit status from Vagrant
exit /b %VAGRANT_EXIT_STATUS%
